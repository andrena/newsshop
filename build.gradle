plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.1.0'
    id "org.owasp.dependencycheck" version "12.1.1"
    id 'com.github.ben-manes.versions' version '0.52.0'
}

node {
    version = '20.12.0'
    npmVersion = '10.5.0'
    download = true
    workDir = project.layout.buildDirectory.dir("nodejs").get().asFile
    npmWorkDir = project.layout.buildDirectory.dir("npm").get().asFile
    nodeProjectDir = file("${project.projectDir}/frontend")
}


group = 'com.andrena'
version = '0.0.1-SNAPSHOT'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    examplesImplementation.extendsFrom testImplementation
}

sourceSets {
    examples {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = [file('src/examples/java')]
        }
        resources {
            srcDirs = [file('src/examples/resources')]
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.h2database:h2'
    implementation 'com.thoughtworks.xstream:xstream:1.4.6'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    examplesImplementation 'org.seleniumhq.selenium:selenium-java:4.32.0'
    examplesImplementation 'org.junit.platform:junit-platform-engine:1.11.4'
    examplesImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    examplesImplementation 'io.github.bonigarcia:webdrivermanager:6.1.0'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
}

tasks.register('buildFrontend', NpmTask) {
    dependsOn npmInstall
    args = ['run', 'build']
    workingDir = file('frontend')
}

tasks.named('processResources') {
    dependsOn buildFrontend
    from('frontend/dist') {
        into('static')
    }

}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootRun') {
    jvmArgs = [
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
            '--add-opens', 'java.base/java.text=ALL-UNNAMED',
            '--add-opens', 'java.desktop/java.awt.font=ALL-UNNAMED',
            '--add-opens', 'java.desktop/java.beans=ALL-UNNAMED',
            '-Xmx512m'
    ]
}
